*The goal of this coursework is to use ODS SELECT and ODS EXCLUDE statements to limit printed results, to create data sets with the ODS OUTPUT statement and to apply style changes through the STYLE= option;

/*1. Create an rtf file that contains the following:*/
ods RTF file='/home/u63982448/Bios511/Output/lab8_730786654.rtf' startpage=no;
ods trace on;

/*a. Run a PROC FREQ on the living_data data set in the course data folder. 
    i. Suppress the printing of the procedure title using the statement: ODS NOPROCTITLE;.
    ii. Request the Fisherâ€™s Exact test for the combination of treatment and statuas
    iii. Include only the Fisher table in the rtf file. */
title 'Q1a fisher test table';
ODS NOPROCTITLE;
ODS SELECT FishersExact;
PROC FREQ data=course.living_data;
    tables treatment*status / fisher;
   
run;
  
/*b. Run a PROC PRINT on the prostate data set in the course folder. 
    i. The results should be grouped by the treatment variable. HINT: Sort the data.
    ii. Print each group on its own page. HINT: Use a BY statement and the appropriate option in an ODS RTF statement.
    iii. In VAR statements, include patient number, serum hemoglobin, serum pap, and tumor size. REMEMBER to close the open ODS destination.*/
title 'Q1b variables analysis by treatment in prostate';
proc sort data=course.prostate out=prostate;
    by treatment;
run;
ODS RTF startpage=bygroup;    
PROC PRINT data=prostate;
    by treatment;
    var patientnumber serumhemoglobin serumpap tumorsize;
run;

ods trace off;
ods RTF close;

/*2. Create a pdf file that contains the following:
a. Apply a style template to the entre pdf document. Use any style except for Pearl, the default. */
ods PDF file='/home/u63982448/Bios511/Output/lab8_730786654.pdf' style=meadowprinter ;
ods trace on;

/*b. Run a PROC UNIVARIATE step on the drivers data set in the course folder. 
    i. Use an ODS EXCLUDE statement to suppress the printing off all of the output generated by PROC UNIVARIATE.
    ii. Use an ODS OUTPUT statement to create a data set for the test for locations values.
    iii. Use the drivers variable as the analysis variable.
  c. Use an ODS SELECT statement to turn printing of output back on.
  d. Print the data set created in #b.*/
title 'Q2bcd analysis on drivers data set';
ODS EXCLUDE all;
ODS OUTPUT TestsForLocation=t_location;
PROC UNIVARIATE data=course.drivers;
    var drivers;
run;

ODS SELECT all;
proc print data=t_location;
run;

/*e. Run a PROC MEANS step on the prostate data set in the course data folder. 
    i. Use an ODS OUTPUT statement to create a data set with the summary information.
    ii. Do not specify any statistics on the PROC MEANS step, which means the default statistics will be calculated and printed.
    iii. Use serum hemoglobin as the analysis variable.
    iv. Use an OUTPUT OUT= statement within the procedure to create a data set. Do not specify any statistics on this statement so that the default statistics are calculated, and the default data structure is used.*/
title 'Q2e analysis on prostate data set';
ODS OUTPUT summary=summaryinfo;

PROC MEANS data=course.prostate ;
    output out= prostate ;
    var serumhemoglobin;     

run;

     
/*f. Use two PROC PRINT steps.
    i. Print the data set created by the ODS OUTPUT statement in #e.
    ii. Print the data set created by the OUTPUT OUT= statement in #e.
        You should have three sets of the same statistics; each presented a slightly different way.  REMEMBER to close the open ODS destination.*/
title 'Q2f output on Q2e';
PROC PRINT data=summaryinfo;
run;

PROC PRINT data=prostate;
run;
